allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = 'org.heat'
    version = '0.1-SNAPSHOT'

    ext.lombok = '1.14.0'
    ext.junit = '4.11'
    ext.mockito = '1.9.5'
    ext.rocket = '1.1-beta9'
    ext.acara = '1.3'
    ext.netty = '4.0.+'
    ext.logback = '1.1.2'
    ext.jbossMarshalling = '1.4.7.Final'
    ext.pgjdbc = '0.3'
    ext.hikari = '2.0.0'
    ext.heatUtil = '0.3.+'
    ext.heatShared = '0.1-SNAPSHOT'
    ext.heatD2vendor = '2.21.3-+'

    repositories {
        maven {
            url 'http://archiva.gitlab.servers.loadlow.me/repository/internal'
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
        maven {
            url 'http://archiva.gitlab.servers.loadlow.me/repository/snapshots'
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
    }

    configurations {
        provided
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {
        provided "org.projectlombok:lombok:${ext.lombok}"

        testCompile "junit:junit:${ext.junit}"
        testCompile "org.mockito:mockito-core:${ext.mockito}"
        testCompile "org.heat:util-testing:${ext.heatUtil}"
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
        test { compileClasspath += configurations.provided }
    }

    idea { module { scopes.PROVIDED.plus += configurations.provided } }

    test {
        workingDir rootProject.projectDir
    }
}


/// Migrations (see: gradle.properties.dist)
buildscript {
    repositories {
        maven {
            url 'http://archiva.gitlab.servers.loadlow.me/repository/internal'
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
    }

    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:3.0"
        classpath 'org.postgresql:postgresql:9.3-1102-jdbc41'
    }
}

apply plugin: 'flyway'

flyway {
    initVersion = 0
}
