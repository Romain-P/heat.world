allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = 'org.heat'
    version = '0.1-SNAPSHOT'

    ext.lombok = '1.14.0'
    ext.junit = '4.11'
    ext.mockito = '1.9.5'
    ext.rocket = '1.1-beta9'
    ext.acara = '1.3'
    ext.netty = '4.0.+'
    ext.logback = '1.1.2'
    ext.jbossMarshalling = '1.4.7.Final'
    ext.pgjdbc = '0.3'
    ext.hikari = '2.0.0'
    ext.guice = '3.0'
    ext.heatUtil = '0.4.+'
    ext.heatShared = '0.1-SNAPSHOT'
    ext.heatD2vendor = '2.21.3-4'

    repositories {
        maven {
            url archivaInternal
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
        maven {
            url archivaSnapshots
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
    }

    configurations {
        provided
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {
        provided "org.projectlombok:lombok:$lombok"

        testCompile "junit:junit:$junit"
        testCompile "org.mockito:mockito-core:$mockito"
        testCompile "org.heat:util-testing:$heatUtil"
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
        test { compileClasspath += configurations.provided }
    }

    idea { module { scopes.PROVIDED.plus += configurations.provided } }

    test {
        workingDir rootProject.projectDir
    }
}


/// Migrations (see: gradle.properties.dist)
buildscript {
    repositories {
        maven {
            url archivaInternal
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
    }

    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:3.0"
        classpath 'org.postgresql:postgresql:9.3-1102-jdbc41'
    }
}

apply plugin: 'flyway'

flyway {
    initVersion = 0
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
