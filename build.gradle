allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = 'org.heat'
    version = '0.1-SNAPSHOT'

    repositories {
        maven {
            url 'http://archiva.gitlab.servers.loadlow.me/repository/internal'
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
        maven {
            url 'http://archiva.gitlab.servers.loadlow.me/repository/snapshots'
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
    }

    configurations {
        provided
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {
        provided 'org.projectlombok:lombok:1.14.0'

        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.heat:util-testing:0.2.1'
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
        test { compileClasspath += configurations.provided }
    }

    idea { module { scopes.PROVIDED.plus += configurations.provided } }

    test {
        workingDir rootProject.projectDir
    }
}


/// Migrations (see: gradle.properties.dist)
buildscript {
    repositories {
        maven {
            url 'http://archiva.gitlab.servers.loadlow.me/repository/internal'
            credentials {
                username archivaUsername
                password archivaPassword
            }
        }
    }

    dependencies {
        classpath "org.flywaydb:flyway-gradle-plugin:3.0"
        classpath 'org.postgresql:postgresql:9.3-1102-jdbc41'
    }
}

apply plugin: 'flyway'

flyway {
    initVersion = 0
}
